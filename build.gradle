// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.2.2' apply false
    id 'com.android.library' version '7.2.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id 'com.google.devtools.ksp' version '1.7.10-1.0.6' apply false
}


task clean(type: Delete) {
    delete rootProject.buildDir
}

// load local.properties
Properties properties = new Properties()
if (project.file('local.properties').exists()) {
    properties.load(project.file('local.properties').newDataInputStream())
    properties.entrySet().each {
        project.ext.set(it.key, it.value)
    }
}

subprojects { project ->
    afterEvaluate {
        if (project.extensions.findByType(com.android.build.gradle.LibraryExtension.class) != null) {
            project.android.libraryVariants.all { variant ->
                def outputFolder = new File("build/generated/ksp/${variant.name}/kotlin")
                variant.addJavaSourceFoldersToModel(outputFolder)
                android.sourceSets.getAt(variant.name).java {
                    srcDir(outputFolder)
                }
            }
        } else if (project.extensions.findByType(com.android.build.gradle.AbstractAppExtension.class) != null) {
            project.android.applicationVariants.all { variant ->
                def outputFolder = new File("build/generated/ksp/${variant.name}/kotlin")
                variant.addJavaSourceFoldersToModel(outputFolder)
                android.sourceSets.getAt(variant.name).java {
                    srcDir(outputFolder)
                }
            }
        }
    }
}